buildscript {
	repositories {
		jcenter()
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
		maven {
			name = "Forge"
			url = "https://files.minecraftforge.net/maven/"
		}
		maven { 
			name = "Jitpack"
			url 'https://jitpack.io/'
		}
	}
	dependencies {
        classpath 'com.github.Chocohead:fabric-loom:415c6b'
	}
}

apply plugin: "fabric-loom"
import net.fabricmc.loom.task.RemappingJar;

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "Manningham Mills"
version = "2.0.1"

minecraft {
	refmapName = "mixins.mm.refmap.json"
	AT = file("example/resources/access-transformations.txt")
}

dependencies {
	minecraft "com.mojang:minecraft:1.14 Pre-Release 3"
	mappings "net.fabricmc:yarn:1.14 Pre-Release 3+build.1:tiny@gz"
	modCompile "net.fabricmc:fabric-loader:0.7.0+build.171"
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'resources'
		}
	}
	example {
		compileClasspath += main.compileClasspath + main.output
		annotationProcessorPath = main.annotationProcessorPath
		java {
			srcDir 'example/src'
		}
		resources {
			srcDir 'example/resources'
			exclude "access-transformations.txt"
		}
	}
}

jar {
	AT.include = false
}

task exampleJar(type: RemappingJar, dependsOn: exampleClasses) {
	classifier = "example"
	from sourceSets.example.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

task exampleSourcesJar(type: RemappingJar, dependsOn: exampleClasses) {
	classifier = "example-sources"
	from sourceSets.example.allSource
}

build.dependsOn exampleJar, exampleSourcesJar

artifacts {
	archives(remapJar) {
		builtBy remapJar
	}
	archives(exampleJar) {
		builtBy exampleJar
	}
	archives(sourcesJar) {
		builtBy sourcesJar
	}
	archives(exampleSourcesJar) {
		builtBy exampleSourcesJar
	}
}
