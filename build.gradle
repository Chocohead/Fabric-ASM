buildscript {
	repositories {
		jcenter()
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
		maven {
			name = "Forge"
			url = "https://files.minecraftforge.net/maven/"
		}
		maven { 
			name = "Jitpack"
			url 'https://jitpack.io/'
		}
	}
	dependencies {
        classpath 'com.github.Chocohead:fabric-loom:5ed2377'
	}
}

apply plugin: "fabric-loom"
import net.fabricmc.loom.task.RemappingJar;

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "Manningham Mills"
version = "1.5"

minecraft {
	refmapName = "mixins.mm.refmap.json"
	AT = file("example/resources/access-transformations.txt")
}

dependencies {
	minecraft "com.mojang:minecraft:1.14 Pre-Release 3"
	mappings "net.fabricmc:yarn:1.14 Pre-Release 3+build.1:tiny@gz"
	modCompile "net.fabricmc:fabric-loader:0.4.1+build.126"
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
		resources {
			srcDir 'resources'
		}
	}
	example {
		compileClasspath += main.compileClasspath + main.output 
		java {
			srcDir 'example/src'
		}
		resources {
			srcDir 'example/resources'
			exclude "access-transformations.txt"
		}
	}
}

jar {
	AT.include = false
}

task exampleJar(type: RemappingJar, dependsOn: exampleClasses) {
	classifier = "example"
	from sourceSets.example.output
}

build.dependsOn exampleJar

artifacts {
    archives(exampleJar) {
		builtBy exampleJar
	}
}
